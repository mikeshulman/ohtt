{-# OPTIONS --exact-split --type-in-type --rewriting --two-level --without-K --no-import-sorts --no-projection-like #-}

module HOTT.Universe where

open import HOTT.Base
open import HOTT.Id

infix 30 _â†“

------------------------------
-- Amazing right adjoints
------------------------------

postulate
  âˆš : {@â™­ I : Type} (@â™­ A : (iâ‚€ iâ‚ : I) (iâ‚‚ : iâ‚€ ï¼ iâ‚) â†’ Type) â†’ I â†’ Type
  dig : {@â™­ I : Type} {@â™­ A : (iâ‚€ iâ‚ : I) (iâ‚‚ : iâ‚€ ï¼ iâ‚) â†’ Type} {iâ‚€ iâ‚ : I} {iâ‚‚ : iâ‚€ ï¼ iâ‚}
    {sâ‚€ : âˆš A iâ‚€} {sâ‚ : âˆš A iâ‚} (sâ‚‚ : Id (âˆš A) iâ‚‚ sâ‚€ sâ‚) â†’
    A iâ‚€ iâ‚ iâ‚‚
  bury : {@â™­ I : Type} (@â™­ A : (iâ‚€ iâ‚ : I) (iâ‚‚ : iâ‚€ ï¼ iâ‚) â†’ Type) {@â™­ K : Type} (@â™­ j : K â†’ I)
    (@â™­ d : (kâ‚€ kâ‚ : K) (kâ‚‚ : kâ‚€ ï¼ kâ‚) â†’ A (j kâ‚€) (j kâ‚) (ap j kâ‚‚)) â†’
    (k : K) â†’ âˆš A (j k)
  -- The computation rules require better Id behavior, so we postpone them to later.

------------------------------
-- The universe
------------------------------

-- Morally, the definition of bitotal correspondence should be
{-
ï¼ˆ trâ‡’ â¦‚ A â‡’ B ï¼‰Ã— ï¼ˆ trâ‡ â¦‚ B â‡’ A ï¼‰Ã— ï¼ˆ rel â¦‚ A â‡’ B â‡’ Type ï¼‰Ã—
  (ï¼ˆ x â¦‚ A ï¼‰â‡’ rel âˆ™ x âˆ™ (trâ‡’ âˆ™ x)) Ã— ( ï¼ˆ y â¦‚ B ï¼‰â‡’ rel âˆ™ (trâ‡ âˆ™ y) âˆ™ y)
-}
-- However, nested Î£-types seem to make Agda really slow, possibly
-- because our Î£-types were postulates and so all the parameters had
-- to be carried around as implicit arguments to fst, snd, and _,_.
-- Thus, instead we define bitotal correspondences to be a record.

-- (TODO: Is this still true now that we've made Î£ a datatype?  Could
-- we go back to using a Î£-type here?)

-- As a mnemonic, whenever a coercion- or transport-like operation
-- belongs to a â‡’, so that it has to be applied with âˆ™, we name it
-- with a â‡’ or a â‡.

record _â‰Š_ (A B : Type) : Type where
  constructor â‰Š[_,_,_,_,_]
  field
    _ï¼_ï½_ : A â†’ B â†’ Type
    coeâ‡’ : A â‡’ B
    coeâ‡ : B â‡’ A
    pushâ‡’ : ï¼ˆ a â¦‚ A ï¼‰â‡’ _ï¼_ï½_ a (coeâ‡’ âˆ™ a)
    pushâ‡ : ï¼ˆ b â¦‚ B ï¼‰â‡’ _ï¼_ï½_ (coeâ‡ âˆ™ b) b
open _â‰Š_ public

[â‰Š] : (Xâ‚€ Xâ‚ : Type) (Xâ‚‚ : Xâ‚€ ï¼ Xâ‚) â†’ Type
[â‰Š] Xâ‚€ Xâ‚ Xâ‚‚ = Xâ‚€ â‰Š Xâ‚

-- This means that the rules for computing ï¼, Id, trâ‡’, etc. in
-- Î£-types don't apply to _â‰Š_, so for the theory to be complete, we'll
-- need to give separate rules for it.  Perhaps we can compute those
-- to the nested Î£-type that they would have been if it were a nested
-- Î£-type.  Or, if we'll need to actually use them in practice, maybe
-- we should make them a record too, and only go to Î£-types at the
-- next level.

------------------------------
-- Kan cubical structure
------------------------------

-- Now, as we will see, the following simple postulate equips all
-- types with Kan cubical structure.
postulate
  kan : (X : Type) â†’ âˆš [â‰Š] X

_â†“ : {Xâ‚€ Xâ‚ : Type} (Xâ‚‚ : Xâ‚€ ï¼ Xâ‚) â†’ Xâ‚€ â‰Š Xâ‚
_â†“ {Xâ‚€} {Xâ‚} Xâ‚‚ = dig {Type} {[â‰Š]} {Xâ‚€} {Xâ‚} {Xâ‚‚} {kan Xâ‚€} {kan Xâ‚} (ap kan {Xâ‚€} {Xâ‚} Xâ‚‚)

-- Computationally, we regard "kan" (informally) as a DESTRUCTOR of a
-- COINDUCTIVE UNIVERSE.  This means that whenever we introduce a map
-- into the universe (i.e. a type constructor), we must specify how
-- kan computes on it.  Since the codomain of kan is a âˆš-type, the
-- result of this computation will generally be a "bury".  (Note that
-- semantically, âˆš-types have Î·-laws, whether or not we can enforce
-- these syntactically, so it is reasonable to compute to a literal
-- "bury" term.)  Giving such a computation law for a particular type
-- former amounts to specifying its identity types along with its
-- transport and lifting, which will generally be instances of the
-- same type former (so that this is morally a corecursive definition,
-- matching the coinductive nature of the universe).

-- This also means that ap-kan, ap-ap-kan, and so on ought also to be
-- regarded as coinductive destructors (of ï¼U, SqU, and so on).  In
-- particular, the computation laws for "kan" on type-formers that
-- produce "bury"s should lift to computation laws of ap-kan on
-- ap-type-formers that produce "ap-bury"s, while the latter compute
-- to "bury"s for the ï¼-âˆš (and thus the "dig" of ï¼-âˆš, which is
-- ap-dig, computes on them).

-- I haven't tried yet in Agda to specify rewrite rules for all of
-- these computations at once.  Perhaps we can define all the
-- "apâ¿-kan"s as an â„•áµ‰-indexed family.

-- The behavior of ap-ap-kan on symmetry is simply given by the
-- ordinary rules of ap-ap on symmetry, together with the definition
-- of symmetry on âˆš-types.  As we will see, this specifies precisely
-- the primitive symmetrized squares that we need.

-- Finally, the fact that ap-kan is (informally) the destructor of a
-- coinductive ï¼U means that it's sensible to add an additional
-- constructor of ï¼U as long as we specify how ap-kan computes on it.
-- This will be  the "promotion" rule from one-to-one correspondences.

-- Intuitively, we can say that while Book HoTT specifies âˆ-groupoid
-- structure *inductively*, and cubical type theory specifies it
-- *explicitly*, HOTT specifies it *coinductively*.

--------------------------------------------------
-- Comparing correspondences to identifications
--------------------------------------------------

-- The correspondence component of ((ap B e) â†“) is (Id B e), while the
-- other four components are transport and lifting, and similarly for
-- (refl B â†“) and (_ï¼_ {B}).  Morally, we regard these as
-- "definitions" of Id and ï¼.  However, to actually "define" ï¼ that
-- way (in the sense of rewriting (_ï¼_ {B}) to part of (refl B â†“))
-- would almost certainly be horribly circular, so we rewrite it in
-- the other direction.

postulate
  reflâ†“ : (A : Type) â†’
    -- _ï¼_ï½_ (refl A â†“) â‰¡
    _ï¼_ï½_ (dig {Type} {[â‰Š]} {A} {A} {refl A} {kan A} {kan A} (refl (kan A))) â‰¡
    _ï¼_ {A}
{-# REWRITE reflâ†“ #-}

-- Because of the direction we compute in reflâ†“, for confluence we
-- need to give explicit analogues for ï¼ of all the relevant rules
-- for refl.  On constructors of the universe, i.e. type formers, this
-- is all over the place.  What remains is eliminators mapping into
-- the universe.

postulate
  ï¼-âˆ™ : {A : Type} (f : A â‡’ Type) (a : A) (xâ‚€ xâ‚ : f âˆ™ a) â†’
    (xâ‚€ ï¼ xâ‚) â‰¡ ((refl f âˆ™ (a , a , refl a) â†“) ï¼ xâ‚€ ï½ xâ‚)
  -- Note the directions of computation.
  --- refl (f âˆ™ a)             âŸ¼  refl f âˆ™ (a , a , refl a)                [by refl-âˆ™]
  --- xâ‚€ ~[ refl (f âˆ™ a) ] xâ‚  âŸ¼  (_ï¼_ {f âˆ™ a} xâ‚€ xâ‚)                     [by reflâ†“]
  --- (_ï¼_ {f âˆ™ a} xâ‚€ xâ‚)     âŸ¼  (xâ‚€ ~[ refl f âˆ™ (a , a , refl a) ] xâ‚)   [by ï¼-âˆ™]
  -- Thus, ï¼-âˆ™ restores confluence between refl-âˆ™ and reflâ†“.
  ï¼-fst :  {B : Type â†’ Type} (u : Î£ Type B) (xâ‚€ xâ‚ : fst u) â†’
    (xâ‚€ ï¼ xâ‚) â‰¡ (fst (refl u) â†“ ï¼ xâ‚€ ï½ xâ‚)
  ï¼-snd : {A : Type} (u : A Ã— Type) (xâ‚€ xâ‚ : snd u) â†’
    (xâ‚€ ï¼ xâ‚) â‰¡ (snd (refl u) â†“ ï¼ xâ‚€ ï½ xâ‚)

{-# REWRITE ï¼-âˆ™ ï¼-fst ï¼-snd #-}

-- It's less clear that it's wrong to rewrite (Id B e) to become part
-- of ((ap B e) â†“), but when I tried this Agda spun forever.  Probably
-- it creates a loop somewhere.  So instead we will rewrite ap to Id,
-- in the same direction as refl to ï¼.

postulate
  apâ†“ : {A : Type} (B : A â†’ Type) {aâ‚€ aâ‚ : A} (aâ‚‚ : aâ‚€ ï¼ aâ‚) â†’
    -- _ï¼_ï½_ (ap B aâ‚‚ â†“) â‰¡
    _ï¼_ï½_ (dig {Type} {[â‰Š]} {B aâ‚€} {B aâ‚} {ap B aâ‚‚} {kan (B aâ‚€)} {kan (B aâ‚)} (ap kan (ap B aâ‚‚))) â‰¡
    Id B aâ‚‚
{-# REWRITE apâ†“ #-}

-- Again, we have to give analogues for Id of the computation rules
-- for ap on eliminators.

postulate
  Id-fst : {Î” : Type} {B : Î” â†’ Type â†’ Type} (u : (Î´ : Î”) â†’ Î£ Type (B Î´)) {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚)
    (xâ‚€ : fst (u Î´â‚€)) (xâ‚ : fst (u Î´â‚)) â†’
    Id (Î» Î´ â†’ fst (u Î´)) Î´â‚‚ xâ‚€ xâ‚ â‰¡ (fst (ap u Î´â‚‚) â†“ ï¼ xâ‚€ ï½ xâ‚)
  Id-snd : {Î” : Type} (A : Î” â†’ Type) (u : (Î´ : Î”) â†’ Î£ (A Î´) (Î» _ â†’ Type)) {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚)
    (xâ‚€ : snd (u Î´â‚€)) (xâ‚ : snd (u Î´â‚)) â†’
    Id (Î» Î´ â†’ snd (u Î´)) Î´â‚‚ xâ‚€ xâ‚ â‰¡ (snd (ap u Î´â‚‚) â†“ ï¼ xâ‚€ ï½ xâ‚)
  Id-âˆ™ : {Î” : Type} {A : Î” â†’ Type} (f : (Î´ : Î”) â†’ A Î´ â‡’ Type)
    (a : (Î´ : Î”) â†’ A Î´) {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚)
    (xâ‚€ : f Î´â‚€ âˆ™ a Î´â‚€) (xâ‚ : f Î´â‚ âˆ™ a Î´â‚) â†’
    Id (Î» Î´ â†’ f Î´ âˆ™ a Î´) Î´â‚‚ xâ‚€ xâ‚ â‰¡ (ap f Î´â‚‚ âˆ™ (a Î´â‚€ , a Î´â‚ , ap a Î´â‚‚) â†“ ï¼ xâ‚€ ï½ xâ‚)

{-# REWRITE Id-fst Id-snd Id-âˆ™ #-}

-- We also need Id-analogues of the rules for ap on variables.  The
-- one for non-locally-bound variables is Id-const, analogous to
-- ap-const.  (Id-refl and ap-refl are also analogous, both morally
-- admissible but useful to have as primitive.)  The action on bound
-- variables is the analogue of ap-idmap:
postulate
  Id-idmap : {Aâ‚€ Aâ‚ : Type} (Aâ‚‚ : Aâ‚€ ï¼ Aâ‚) (aâ‚€ : Aâ‚€) (aâ‚ : Aâ‚) â†’
    Id (Î» X â†’ X) Aâ‚‚ aâ‚€ aâ‚ â‰¡ (Aâ‚‚ â†“) ï¼ aâ‚€ ï½ aâ‚
{-# REWRITE Id-idmap #-}

------------------------------
-- Functoriality of Id
------------------------------

-- With the computation rules for Id on application, we can prove that
-- its functoriality holds definitionally.  However, this only holds
-- for â‡’-functions rather than framework â†’-functions.  Thus, in other
-- situations we may need to apply these coercions manually, wrapping
-- a type family in ğ›Œ by hand.
â†Id-ap : {A B : Type} (f : A â†’ B) (C : B â‡’ Type)
  {aâ‚€ aâ‚ : A} (aâ‚‚ : aâ‚€ ï¼ aâ‚) {câ‚€ : C âˆ™ f aâ‚€} {câ‚ : C  âˆ™ f aâ‚} â†’
  Id (Î» a â†’ C âˆ™ f a) aâ‚‚ câ‚€ câ‚ â†’ Id (C âˆ™_) (ap f aâ‚‚) câ‚€ câ‚
â†Id-ap f C aâ‚‚ e = e

â†’Id-ap : {A B : Type} (f : A â†’ B) (C : B â‡’ Type)
  {aâ‚€ aâ‚ : A} (aâ‚‚ : aâ‚€ ï¼ aâ‚) {câ‚€ : C âˆ™ f aâ‚€} {câ‚ : C  âˆ™ f aâ‚} â†’
  Id (C âˆ™_) (ap f aâ‚‚) câ‚€ câ‚ â†’ Id (Î» a â†’ C âˆ™ f a) aâ‚‚ câ‚€ câ‚
â†’Id-ap f C aâ‚‚ e = e

------------------------------
-- ap-snd and ap-, and ap-âˆ™
------------------------------

-- Now that we have Id-âˆ™ we can tackle these super-difficult aps.  The
-- problem with all of them is that their well-typedness seems to
-- depend on themselves.  However, we can convince Agda to accept them
-- if we build up in stages, first asserting simpler versions with
-- less type dependency.  We also have to interleave this process for
-- all three of them, since they depend on each other as well.

-- We also frequently use the following trick.  The rule Id-âˆ™ only
-- fires on type families that belong to a â‡’ and are applied with âˆ™,
-- but for general rewriting we need these rules to apply to arbitrary
-- type families that belong to a â†’.  So we first define an element of
-- the type we need under the assumption of a â‡’ type family, and then
-- in the actual rewrite rule we hand off with a ğ›Œ-abstraction.
-- (Morally, we are using one of the Id-ap rules from above, but they
-- don't work completely until we have these computation rules for ap
-- in place, so we use special lemmas instead.)

-- First we can state ap-snd for non-dependent product types.
frob-ap-sndÂ¹ : {Î” : Type} (A B : Î” â‡’ Type) (u : (Î´ : Î”) â†’ (A âˆ™ Î´) Ã— (B âˆ™ Î´))
  {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
  Id (B âˆ™_) Î´â‚‚ (snd (u Î´â‚€)) (snd (u Î´â‚))
frob-ap-sndÂ¹ A B u {Î´â‚€} {Î´â‚} Î´â‚‚ = snd (ap u Î´â‚‚)

postulate
  ap-sndÂ¹ : {Î” : Type} {A B : Î” â†’ Type} (u : (Î´ : Î”) â†’ A Î´ Ã— B Î´)
    {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
    ap (Î» Î´ â†’ snd (u Î´)) Î´â‚‚ â‰¡ frob-ap-sndÂ¹ (ğ›Œ A) (ğ›Œ B) u Î´â‚‚
{-# REWRITE ap-sndÂ¹ #-}

-- This allows us to state all three rules for dependent Î - and
-- Î£-types, as long as they don't depend on the context.
frob-ap-sndÂ² : {Î” A : Type} (B : A â‡’ Type)
  (u : (Î´ : Î”) â†’ Î£ A (B âˆ™_)) {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
  Id (Î» z â†’ B âˆ™ (fst (u z))) Î´â‚‚ (snd (u Î´â‚€)) (snd (u Î´â‚))
frob-ap-sndÂ² B u Î´â‚‚ = snd (ap u Î´â‚‚)

frob-ap-âˆ™Â² : {Î” A : Type} (B : A â‡’ Type)
  (f : (Î´ : Î”) â†’ Î  A (B âˆ™_)) (a : (Î´ : Î”) â†’ A)
  {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
  Id (Î» z â†’ B âˆ™ (a z)) Î´â‚‚ (f Î´â‚€ âˆ™ a Î´â‚€) (f Î´â‚ âˆ™ a Î´â‚)
frob-ap-âˆ™Â² {Î”} {A} B f a {Î´â‚€} {Î´â‚} Î´â‚‚ = ap f Î´â‚‚ âˆ™ (a Î´â‚€ , a Î´â‚ , ap a Î´â‚‚)

frob-ap-,Â² : {Î” A : Type} (B : A â‡’ Type)
  (a : (x : Î”) â†’ A) (b : (x : Î”) â†’ B âˆ™ (a x)) {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
  Id {Î” Ã— A} (Î» u â†’ B âˆ™ (snd u)) {Î´â‚€ , a Î´â‚€} {Î´â‚ , a Î´â‚} (Î´â‚‚ , ap a Î´â‚‚) (b Î´â‚€) (b Î´â‚)
frob-ap-,Â² B a b Î´â‚‚ = ap b Î´â‚‚

postulate
  ap-sndÂ² : {Î” A : Type} (B : A â†’ Type)
    (u : (Î´ : Î”) â†’ Î£ A B) {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
    ap (Î» Î´ â†’ snd (u Î´)) Î´â‚‚ â‰¡ frob-ap-sndÂ² (ğ›Œ B) u Î´â‚‚
  ap-âˆ™Â² : {Î” A : Type} (B : A â†’ Type)
    (f : (Î´ : Î”) â†’ Î  A B) (a : (Î´ : Î”) â†’ A)
    {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
    ap (Î» Î´ â†’ f Î´ âˆ™ a Î´) Î´â‚‚ â‰¡ frob-ap-âˆ™Â² (ğ›Œ B) f a Î´â‚‚
  ap-,Â² : {Î” A : Type} (B : A â†’ Type)
    (a : (x : Î”) â†’ A) (b : (x : Î”) â†’ B (a x)) {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
    ap (Î» x â†’ (_,_ {A} {B} (a x) (b x))) Î´â‚‚ â‰¡ (ap a Î´â‚‚ , frob-ap-,Â² (ğ›Œ B) a b Î´â‚‚)
{-# REWRITE ap-sndÂ² ap-âˆ™Â² ap-,Â² #-}

-- These, in turn, allow us to state the general forms of all three rules.
frob-ap-snd : {Î” : Type} (A : Î” â‡’ Type) (B : ï¼ˆ x â¦‚ Î” ï¼‰â‡’ A âˆ™ x â‡’ Type)
  (u : (Î´ : Î”) â†’ Î£ (A âˆ™ Î´) (B âˆ™ Î´ âˆ™_)) {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
  Id (Î» z â†’ B âˆ™ z âˆ™ (fst (u z))) Î´â‚‚ (snd (u Î´â‚€)) (snd (u Î´â‚))
frob-ap-snd A B u Î´â‚‚ = snd (ap u Î´â‚‚)

frob-ap-, : {Î” : Type} (A : Î” â‡’ Type) (B : Î£ Î” (A âˆ™_) â‡’ Type)
  (a : (x : Î”) â†’ A âˆ™ x) (b : (x : Î”) â†’ B âˆ™ (x , a x)) {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
  Id (B âˆ™_) (Î´â‚‚ , ap a Î´â‚‚) (b Î´â‚€) (b Î´â‚)
frob-ap-, A B a b Î´â‚‚ = ap b Î´â‚‚

frob-ap-âˆ™ : {Î” : Type} (A : Î” â‡’ Type) (B : Î£ Î” (A âˆ™_) â‡’ Type)
  (f : (Î´ : Î”) â†’ Î  (A âˆ™ Î´) (Î» x â†’ B âˆ™ (Î´ , x))) (a : (Î´ : Î”) â†’ A âˆ™ Î´)
  {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
  Id (Î» z â†’ B âˆ™ (z , a z)) Î´â‚‚ (f Î´â‚€ âˆ™ a Î´â‚€) (f Î´â‚ âˆ™ a Î´â‚)
frob-ap-âˆ™ {Î”} A B f a {Î´â‚€} {Î´â‚} Î´â‚‚ = ap f Î´â‚‚ âˆ™ (a Î´â‚€ , a Î´â‚ , ap a Î´â‚‚) 

postulate
  ap-snd : {Î” : Type} (A : Î” â†’ Type) (B : (x : Î”) â†’ A x â†’ Type)
    (u : (Î´ : Î”) â†’ Î£ (A Î´) (B Î´)) {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
    ap (Î» Î´ â†’ snd (u Î´)) Î´â‚‚ â‰¡ frob-ap-snd (ğ›Œ A) (Æ› Î´ â‡’ Æ› a â‡’ B Î´ a) u Î´â‚‚
  ap-, : {Î” : Type} (A : Î” â†’ Type) (B : (x : Î”) â†’ A x â†’ Type)
    (a : (x : Î”) â†’ A x) (b : (x : Î”) â†’ B x (a x)) {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
    ap (Î» x â†’ (_,_ {A x} {B x} (a x) (b x))) Î´â‚‚ â‰¡ (ap a Î´â‚‚ , frob-ap-, (ğ›Œ A) (Æ› z â‡’ B (fst z) (snd z)) a b Î´â‚‚)
  ap-âˆ™ : {Î” : Type} {A : Î” â†’ Type} {B : (Î´ : Î”) â†’ A Î´ â†’ Type}
    (f : (Î´ : Î”) â†’ Î  (A Î´) (B Î´)) (a : (Î´ : Î”) â†’ A Î´)
    {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
    ap (Î» Î´ â†’ f Î´ âˆ™ a Î´) Î´â‚‚ â‰¡ frob-ap-âˆ™ (ğ›Œ A) (Æ› z â‡’ B (fst z) (snd z)) f a Î´â‚‚
{-# REWRITE ap-snd ap-, ap-âˆ™ #-}

------------------------------
-- Transport and lifting
------------------------------

-- The other components of ap-â†“ are transport and lifting.

trâ‡’ : {A : Type} (B : A â†’ Type) {aâ‚€ aâ‚ : A} (aâ‚‚ : aâ‚€ ï¼ aâ‚) â†’ B aâ‚€ â‡’ B aâ‚
trâ‡’ {A} B {aâ‚€} {aâ‚} aâ‚‚ = coeâ‡’ (ap B aâ‚‚ â†“)

trâ‡ : {A : Type} (B : A â†’ Type) {aâ‚€ aâ‚ : A} (aâ‚‚ : aâ‚€ ï¼ aâ‚) â†’ B aâ‚ â‡’ B aâ‚€
trâ‡ {A} B {aâ‚€} {aâ‚} aâ‚‚ = coeâ‡ (ap B aâ‚‚ â†“)

liftâ‡’ : {A : Type} (B : A â†’ Type) {aâ‚€ aâ‚ : A} (aâ‚‚ : aâ‚€ ï¼ aâ‚) â†’
  ï¼ˆ bâ‚€ â¦‚ B aâ‚€ ï¼‰â‡’ Id B aâ‚‚ bâ‚€ (trâ‡’ B aâ‚‚ âˆ™ bâ‚€)
liftâ‡’ {A} B {aâ‚€} {aâ‚} aâ‚‚ = pushâ‡’ (ap B aâ‚‚ â†“)

liftâ‡ : {A : Type} (B : A â†’ Type) {aâ‚€ aâ‚ : A} (aâ‚‚ : aâ‚€ ï¼ aâ‚) â†’
  ï¼ˆ bâ‚ â¦‚ B aâ‚ ï¼‰â‡’ Id B aâ‚‚ (trâ‡ B aâ‚‚ âˆ™ bâ‚) bâ‚
liftâ‡ {A} B {aâ‚€} {aâ‚} aâ‚‚ = pushâ‡ (ap B aâ‚‚ â†“)

-- Similarly, the other components of refl-â†“ are "nudges" and
-- "touches", which are trivial up to homotopy, but not definitionally
-- in general (lack of regularity).

nudgeâ‡’ : {A : Type} â†’ A â‡’ A
nudgeâ‡’ {A} = coeâ‡’ (refl A â†“)

nudgeâ‡ : {A : Type} â†’ A â‡’ A
nudgeâ‡ {A} = coeâ‡ (refl A â†“)

touchâ‡’ : {A : Type} â†’ ï¼ˆ a â¦‚ A ï¼‰â‡’ a ï¼ nudgeâ‡’ âˆ™ a
touchâ‡’ {A} = pushâ‡’ (refl A â†“)

touchâ‡ : {A : Type} â†’ ï¼ˆ a â¦‚ A ï¼‰â‡’ nudgeâ‡ âˆ™ a ï¼ a
touchâ‡ {A} = pushâ‡ (refl A â†“)

----------------------------------------
-- Fancier ap-refl and Id-refl
----------------------------------------

-- The problem with Id-refl and ap-refl as rewrites is that (refl a)
-- is volatile, so it may have already been reduced to something else.
-- This is an issue for squares, which are defined as Id-ï¼, where I
-- don't know of another way to enforce Id-refl.  The following
-- rewrites allow us to at least break down the case when refl has
-- been reduced to a tuple of refls.

postulate
  ap-refl, : {A : Type} (B : A â†’ Type) (C : Î£ A B â†’ Type)
    (f : (x : Î£ A B) â†’ C x) (a : A) {bâ‚€ bâ‚ : B a} (bâ‚‚ : bâ‚€ ï¼ bâ‚) â†’
    ap f {a , bâ‚€} {a , bâ‚} (refl a , bâ‚‚) â‰¡
    â†Id-ap {B a} {Î£ A B} (Î» b â†’ (a , b)) (ğ›Œ C) bâ‚‚ (ap (Î» y â†’ f (a , y)) bâ‚‚)
  Id-refl, : {A : Type} (B : A â†’ Type) (C : Î£ A B â†’ Type)
    (a : A) {bâ‚€ bâ‚ : B a} (bâ‚‚ : bâ‚€ ï¼ bâ‚) (câ‚€ : C (a , bâ‚€)) (câ‚ : C (a , bâ‚)) â†’
    Id C {a , bâ‚€} {a , bâ‚} (refl a , bâ‚‚) câ‚€ câ‚ â‰¡ Id (Î» b â†’ C (a , b)) {bâ‚€} {bâ‚} bâ‚‚ câ‚€ câ‚
{-# REWRITE ap-refl, Id-refl, #-}

-- This isn't perfect, even when considering tuples, since it doesn't
-- deal with cases like ((refl a , bâ‚‚) , câ‚‚), which arise naturally
-- due to (for instance) ap-Æ›.  This would be an advantage of using
-- telescopes instead of Î£-types, since a telescope can be maintained
-- as right-associated even when extending it on the right.
