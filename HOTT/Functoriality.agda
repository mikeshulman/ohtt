{-# OPTIONS --exact-split --type-in-type --rewriting --two-level --without-K --no-import-sorts --no-projection-like #-}

module HOTT.Functoriality where

open import HOTT.Base
open import HOTT.Id
open import HOTT.Exonat
open import HOTT.Cube
open import HOTT.Universe.Parametric

------------------------------
-- ap-snd and ap-, and ap-âˆ™
------------------------------

-- Now that we can compute ï¼ and Id using maps into the universe, we
-- can tackle these super-difficult aps.  The problem with all of them
-- is that their well-typedness seems to depend on themselves.
-- However, we can convince Agda to accept them if we build up in
-- stages, first asserting simpler versions with less type dependency.
-- We also have to interleave this process for all three of them,
-- since they depend on each other as well.

-- We also frequently use the following trick.  Rules like ap-âˆ™ only
-- fire on terms that belong to a â‡’ and are applied with âˆ™, but for
-- general rewriting we need these rules to apply to arbitrary type
-- families that belong to a â†’.  So we first define an element of the
-- type we need under the assumption of a â‡’ type family, and then in
-- the actual rewrite rule we hand off with a ğ›Œ-abstraction.

-- The simplest form of ap-âˆ™ is for non-dependent functions between
-- constant types.  In this case we don't need the above trick at all.
postulate
  ap-âˆ™Â¹ : {Î” A B : Type}
    (f : (Î´ : Î”) â†’ A â‡’ B) (a : (Î´ : Î”) â†’ A)
    {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
    ap (Î» Î´ â†’ f Î´ âˆ™ a Î´) Î´â‚‚ â‰¡ ap f Î´â‚‚ âˆ™ (a Î´â‚€ , a Î´â‚ , ap a Î´â‚‚)
{-# REWRITE ap-âˆ™Â¹ #-}

-- With this, we can state ap-snd for non-dependent pairs (in
-- nonconstant type families).
frob-ap-sndÂ¹ : {Î” : Type} (A B : Î” â‡’ Type) (u : (Î´ : Î”) â†’ (A âˆ™ Î´) Ã— (B âˆ™ Î´))
  {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
  Id (B âˆ™_) Î´â‚‚ (snd (u Î´â‚€)) (snd (u Î´â‚))
frob-ap-sndÂ¹ A B u {Î´â‚€} {Î´â‚} Î´â‚‚ = snd (ap u Î´â‚‚) 

postulate
  ap-sndÂ¹ : {Î” : Type} {A B : Î” â†’ Type} (u : (Î´ : Î”) â†’ A Î´ Ã— B Î´)
    {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
    ap (Î» Î´ â†’ snd (u Î´)) Î´â‚‚ â‰¡  frob-ap-sndÂ¹ (ğ›Œ A) (ğ›Œ B) u Î´â‚‚
{-# REWRITE ap-sndÂ¹ #-}

-- This, in turn, allows stating ap-âˆ™ for *dependent* functions in a
-- constant type family, and similarly ap-, for dependent pairs in a
-- constant type family.
frob-ap-âˆ™Â² : {Î” A : Type} (B : A â‡’ Type)
  (f : (Î´ : Î”) â†’ Î  A (B âˆ™_)) (a : (Î´ : Î”) â†’ A)
  {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
  Id (Î» z â†’ B âˆ™ (a z)) Î´â‚‚ (f Î´â‚€ âˆ™ a Î´â‚€) (f Î´â‚ âˆ™ a Î´â‚)
frob-ap-âˆ™Â² {Î”} {A} B f a {Î´â‚€} {Î´â‚} Î´â‚‚ = ap f Î´â‚‚ âˆ™ (a Î´â‚€ , a Î´â‚ , ap a Î´â‚‚)

frob-ap-,Â² : {Î” A : Type} (B : A â‡’ Type)
  (a : (x : Î”) â†’ A) (b : (x : Î”) â†’ B âˆ™ (a x)) {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
  Id {Î” Ã— A} (Î» u â†’ B âˆ™ (snd u)) {Î´â‚€ , a Î´â‚€} {Î´â‚ , a Î´â‚} (Î´â‚‚ , ap a Î´â‚‚) (b Î´â‚€) (b Î´â‚)
frob-ap-,Â² B a b Î´â‚‚ = ap b Î´â‚‚

postulate
  ap-âˆ™Â² : {Î” A : Type} (B : A â†’ Type)
    (f : (Î´ : Î”) â†’ Î  A B) (a : (Î´ : Î”) â†’ A)
    {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
    ap (Î» Î´ â†’ f Î´ âˆ™ a Î´) Î´â‚‚ â‰¡ frob-ap-âˆ™Â² (ğ›Œ B) f a Î´â‚‚
  ap-,Â² : {Î” A : Type} (B : A â†’ Type)
    (a : (x : Î”) â†’ A) (b : (x : Î”) â†’ B (a x)) {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
    ap (Î» x â†’ (_,_ {A} {B} (a x) (b x))) Î´â‚‚ â‰¡
      (ap a Î´â‚‚ , frob-ap-,Â² (ğ›Œ B) a b Î´â‚‚)
{-# REWRITE ap-âˆ™Â² ap-,Â² #-}

-- Now we can handle the fully general ap-, for dependent pairs in a
-- non-constant type family, and also ap-snd for dependent pairs in a
-- constant type family.
frob-ap-, : {Î” : Type} (A : Î” â‡’ Type) (B : Î£ Î” (A âˆ™_) â‡’ Type)
  (a : (x : Î”) â†’ A âˆ™ x) (b : (x : Î”) â†’ B âˆ™ (x , a x)) {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
  Id (B âˆ™_) (Î´â‚‚ , ap a Î´â‚‚) (b Î´â‚€) (b Î´â‚)
frob-ap-, A B a b Î´â‚‚ = ap b Î´â‚‚

frob-ap-sndÂ² : {Î” A : Type} (B : A â‡’ Type)
  (u : (Î´ : Î”) â†’ Î£ A (B âˆ™_)) {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
  Id (Î» z â†’ B âˆ™ (fst (u z))) Î´â‚‚ (snd (u Î´â‚€)) (snd (u Î´â‚))
frob-ap-sndÂ² B u Î´â‚‚ = snd (ap u Î´â‚‚)

postulate
  ap-, : {Î” : Type} (A : Î” â†’ Type) (B : (x : Î”) â†’ A x â†’ Type)
    (a : (x : Î”) â†’ A x) (b : (x : Î”) â†’ B x (a x)) {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
    ap (Î» x â†’ (_,_ {A x} {B x} (a x) (b x))) Î´â‚‚ â‰¡
      (ap a Î´â‚‚ , frob-ap-, (ğ›Œ A) (Æ› z â‡’ B (fst z) (snd z)) a b Î´â‚‚)
  ap-sndÂ² : {Î” A : Type} (B : A â†’ Type)
    (u : (Î´ : Î”) â†’ Î£ A B) {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
    ap (Î» Î´ â†’ snd (u Î´)) Î´â‚‚ â‰¡ frob-ap-sndÂ² (ğ›Œ B) u Î´â‚‚
{-# REWRITE ap-, ap-sndÂ² #-}

-- Next is the fully general ap-âˆ™ for dependent functions in a
-- non-constant type family.
frob-ap-âˆ™ : {Î” : Type} (A : Î” â‡’ Type) (B : Î£ Î” (A âˆ™_) â‡’ Type)
  (f : (Î´ : Î”) â†’ Î  (A âˆ™ Î´) (Î» x â†’ B âˆ™ (Î´ , x))) (a : (Î´ : Î”) â†’ A âˆ™ Î´)
  {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
  Id (Î» z â†’ B âˆ™ (z , a z)) Î´â‚‚ (f Î´â‚€ âˆ™ a Î´â‚€) (f Î´â‚ âˆ™ a Î´â‚)
frob-ap-âˆ™ {Î”} A B f a {Î´â‚€} {Î´â‚} Î´â‚‚ = ap f Î´â‚‚ âˆ™ (a Î´â‚€ , a Î´â‚ , ap a Î´â‚‚)

postulate
  ap-âˆ™ : {Î” : Type} {A : Î” â†’ Type} {B : (Î´ : Î”) â†’ A Î´ â†’ Type}
    (f : (Î´ : Î”) â†’ Î  (A Î´) (B Î´)) (a : (Î´ : Î”) â†’ A Î´)
    {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
    ap (Î» Î´ â†’ f Î´ âˆ™ a Î´) Î´â‚‚ â‰¡
      frob-ap-âˆ™ (ğ›Œ A) (Æ› z â‡’ B (fst z) (snd z)) f a Î´â‚‚
{-# REWRITE ap-âˆ™ #-}

-- And finally, we can deal with fully general ap-snd, for dependent
-- pairs in a non-constant type family.
frob-ap-snd : {Î” : Type} (A : Î” â‡’ Type) (B : ï¼ˆ x â¦‚ Î” ï¼‰â‡’ A âˆ™ x â‡’ Type)
  (u : (Î´ : Î”) â†’ Î£ (A âˆ™ Î´) (B âˆ™ Î´ âˆ™_)) {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
  Id (Î» z â†’ B âˆ™ z âˆ™ (fst (u z))) Î´â‚‚ (snd (u Î´â‚€)) (snd (u Î´â‚))
frob-ap-snd A B u Î´â‚‚ = snd (ap u Î´â‚‚)

postulate
  ap-snd : {Î” : Type} (A : Î” â†’ Type) (B : (x : Î”) â†’ A x â†’ Type)
    (u : (Î´ : Î”) â†’ Î£ (A Î´) (B Î´)) {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚) â†’
    ap (Î» Î´ â†’ snd (u Î´)) Î´â‚‚ â‰¡ frob-ap-snd (ğ›Œ A) (Æ› Î´ â‡’ Æ› a â‡’ B Î´ a) u Î´â‚‚
{-# REWRITE ap-snd #-}

------------------------------
-- ap-corr
------------------------------

-- With these rules for ap available, we can now postulate ap-corr.
-- This requires the implicit equality argument to corr to eliminate
-- green slime and fire, since the codomain of the "ap" may in
-- practice be a reduced version of Corr rather than Corr itself.
-- Since these equalities are under a binder in ap-corr, we need to
-- apply funextáµ‰ before we can destruct them in the output; for this
-- we use an auxiliary function.

frob-ap-corr : {n : â„•áµ‰} {Î” : Type} {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚)
  (A : Î” â†’ CUBE n Type) {Î© : Î” â†’ Type} (Ï‰ : (Î» Î´ â†’ Corr n âˆ™ fst (A Î´)) â‰¡ Î©) â†’
  Id Î© Î´â‚‚ (corr (A Î´â‚€) â¦ƒ happlyáµ‰ Ï‰ Î´â‚€ â¦„) (corr (A Î´â‚) â¦ƒ happlyáµ‰ Ï‰ Î´â‚ â¦„)
frob-ap-corr {n} {Î”} {Î´â‚€} {Î´â‚} Î´â‚‚ A refláµ‰ =
  coeÊ°-Id (Corr n âˆ™_) {Î´â‚‚ = ap (Î» x â†’ fst (A x)) Î´â‚‚} refláµ‰ refláµ‰ reflÊ°
    (cong (Î» x â†’ corr x â¦ƒ refláµ‰ â¦„) (Î·Î£ _ (A Î´â‚€)) )
    (cong (Î» x â†’ corr x â¦ƒ refláµ‰ â¦„) (Î·Î£ _ (A Î´â‚)) )
    (fst (corr {ğ¬ n}
      ((fst (A Î´â‚€) , fst (A Î´â‚)  , fst (ap A Î´â‚‚) ,
        snd (A Î´â‚€) , snd (A Î´â‚)) , snd (ap A Î´â‚‚)) â¦ƒ refláµ‰ â¦„))

postulate
  ap-corr : {n : â„•áµ‰} {Î” : Type} {Î´â‚€ Î´â‚ : Î”} (Î´â‚‚ : Î´â‚€ ï¼ Î´â‚)
    (A : Î” â†’ CUBE n Type) {Î© : Î” â†’ Type} (Ï‰ : (Î´ : Î”) â†’ Corr n âˆ™ fst (A Î´) â‰¡ Î© Î´) â†’
    ap (Î» Î´ â†’ corr {n} (A Î´) â¦ƒ Ï‰ Î´ â¦„) Î´â‚‚ â‰¡ frob-ap-corr Î´â‚‚ A (funextáµ‰ Ï‰) 
{-# REWRITE ap-corr #-}
